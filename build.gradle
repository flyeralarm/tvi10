
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'net.ricebean.tools.colorstrip'
    archivesBaseName = "SmartColorStrip"
    version = '1.1'
    description = "A smart color strip generator."


    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }
}

project(':cli') {
    dependencies {
        compile "commons-cli:commons-cli:1.3.1"
        compile project(':lib')
    }

    jar {
        archiveName 'tvi10cli.jar'

        from {
            (configurations.runtime).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        manifest {

            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": version,
                    "Main-Class": "net.ricebean.tools.tvi10.cli.App",
            )
        }
    }
}

project(':lib') {

    dependencies {
        compile ('com.lowagie:itext:2.1.7') {
            exclude group: 'org.bouncycastle', module: 'bcmail-jdk14'
            exclude group: 'bouncycastle', module: 'bcmail-jdk14'
            exclude group: 'org.bouncycastle', module: 'bctsp-jdk14'
            exclude group: 'bouncycastle', module: 'bcprov-jdk14'
        }
        compile 'commons-io:commons-io:2.5'
    }

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from tasks.javadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar

        archives javadocJar
        archives sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'SmartColorStrip'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Generate smart color strips.'
                    url 'https://github.com/ricebean-net/SmartColorStrip'

                    scm {
                        connection 'scm:git@github.com:ricebean-net/SmartColorStrip.git'
                        developerConnection 'scm:git@github.com:ricebean-net/SmartColorStrip.git'
                        url 'https://github.com/ricebean-net/SmartColorStrip.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'meixxi'
                            name 'Stefan Meissner'
                        }
                    }
                }
            }
        }
    }
}


